import jaci.openrio.gradle.wpi.dependencies.WPIToolsPlugin

jar.baseName = 'MercuryWidgets'

def shuffleboardLogDir = "/shuffleboard"

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Phoenix
// and NavX.
repositories {
    mavenCentral()
    maven {
        url "http://first.wpi.edu/FRC/roborio/maven/release"
    }
}

dependencies {
    compile 'edu.wpi.first.shuffleboard:api:+'
}

task debugShuffleboard(type: Exec) {
    group = "GradleRIO"
    description = "Launch Shuffleboard with logging"

    // Lazy config, just pull wpiTools config from robot proj.
    def config = project(':robot').configurations.getByName("wpiTools")
    def byteOut = new ByteArrayOutputStream()

    // Get shuffleboard dependency from wpiTools config
    Set<File> jarfiles = config.files(config.dependencies.find { d -> d.group == "edu.wpi.first.shuffleboard" })

    // Exec task fields
    workingDir = WPIToolsPlugin.shuffleboardDirectory()
    commandLine 'java', '-jar', "${jarfiles.first().absolutePath}"
    standardOutput = byteOut
    errorOutput = byteOut

    doFirst {
        System.out.println("NOTICE: Logging!")
        System.out.println("If you do not need to debug, you should be using :robot:shuffleboard!")
    }

    // Do logging after task finishes
    // i.e.: Shuffleboard is closed
    doLast {
        def date = new Date().format('yyy-MM-dd HH-mm-ss.SSS')
        def logDir = new File("${rootProject.projectDir}/.log", shuffleboardLogDir)

        if (!logDir.exists())
            logDir.mkdirs()

        def file = new File(logDir, "shuffleboard-${date}.log")

        def fileOut = new FileOutputStream(file)

        byteOut.writeTo(fileOut)

        fileOut.close()

        System.out.println("Log exported to ${file.toString()}.")
    }
}